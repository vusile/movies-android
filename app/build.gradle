plugins {
    id 'com.android.application'
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdk 30

    defaultConfig {
        applicationId "com.moviestanzania"
        minSdk 21
        targetSdk 30
        versionCode 6
        versionName "6.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
    }

    flavorDimensions "environment"

    productFlavors {
        live {
            dimension "environment"
        }
        local {
            dimension "environment"
            applicationIdSuffix ".local"
        }
    }

    applicationVariants.all { variant ->
        def apiBase
        def homeAdUnitId
        def detailAdUnitId

        def liveApiBase = "https://moviestanzania.com/api/"
        def liveHomeAdUnitId = "ca-app-pub-7910170887251044/5488540606"
        def liveDetailAdUnitId = "ca-app-pub-7910170887251044/1018317716"

        def testingApiBase = "http://10.0.2.2:8000/api/"
        def testingHomeAdUnitId = "ca-app-pub-3940256099942544/6300978111"
        def testingDetailAdUnitId = "ca-app-pub-3940256099942544/6300978111"

        if(variant.applicationId.contains("debug")) {
            if(variant.applicationId.contains("local")) {
                apiBase = testingApiBase
            } else {
                apiBase = liveApiBase
            }
            homeAdUnitId = testingHomeAdUnitId
            detailAdUnitId = testingDetailAdUnitId
        } else {
            apiBase = liveApiBase
            homeAdUnitId = liveHomeAdUnitId
            detailAdUnitId = liveDetailAdUnitId
        }

        buildConfigField "String", "API_BASE", "\"" + apiBase + "\""
        buildConfigField "String", "HOME_AD_UNIT_ID", "\"" + homeAdUnitId + "\""
        buildConfigField "String", "DETAIL_AD_UNIT_ID", "\"" + detailAdUnitId + "\""
    }



    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation platform('com.google.firebase:firebase-bom:28.4.0')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation 'com.koushikdutta.ion:ion:3.1.0'
    implementation 'com.pierfrancescosoffritti.androidyoutubeplayer:core:10.0.5'
    implementation 'com.google.android.gms:play-services-ads:20.4.0'
}